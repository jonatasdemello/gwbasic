'<Digger. The classic Digger game re-created in ASCII>
'Copyright (C) <2014> <Navin Ipe>
'Contact: navinipe at gmail dot com

'This program is free software: you can redistribute it and/or modify
'it under the terms of the GNU General Public License as published by
'the Free Software Foundation, either version 3 of the License, or
'(at your option) any later version.

'This program is distributed in the hope that it will be useful,
'but WITHOUT ANY WARRANTY; without even the implied warranty of
'MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
'GNU General Public License for more details.

'You should have received a copy of the GNU General Public License
'along with this program. If not, see <http://www.gnu.org/licenses/>.

10 '----------ADVANCED DIGGER
20 GOSUB 3870: GOSUB 4150: GOSUB 1750: GOSUB 2810
30 CLS : KEY OFF: SCREEN 2: SCREEN 0, 0, 0: LEVEL = 1: LIFE = 3: SHOT = 2
40 '-INTRO SCREEN
50 COLOR 6: LOCATE 22, 1: PRINT CHR$(200); STRING$(78, CHR$(205)); CHR$(188)
60 R = 21: C = 1
70 LOCATE R, C: PRINT CHR$(186); STRING$(78, CHR$(176)); CHR$(186)
80 IF R = 3 THEN 90 ELSE R = R - 1: GOTO 70
90 LOCATE 2, 1: PRINT CHR$(201); STRING$(78, CHR$(205)); CHR$(187)
100 LOCATE 5, 20: PRINT CHR$(19): LOCATE 5, 60: PRINT CHR$(19)
110 LOCATE 12, 40: PRINT CHR$(19)
120 LOCATE 20, 20: PRINT STRING$(50, CHR$(46))
130 R = 20: C = 70
140 LOCATE R, C: PRINT "."
150 IF R = 7 THEN 160 ELSE R = R - 1: GOTO 140
160 '--PRINTING DIAM
170 COLOR 12
180 IVAL = LEVEL * 10: DIA = 0
190 LOCATE 3, 5: COLOR 12: PRINT CHR$(4): COLOR 10
200 FOR I = 1 TO IVAL
210 READ B, C
220 LOCATE B, C: COLOR 12: PRINT CHR$(4): COLOR 10
230 NEXT I
240 COLOR 10
250 S1R = 5: S2R = 5: S1C = 20: S2C = 60: S3R = 12: S3C = 40: S1$ = CHR$(19)
260 '-- OTHER VALUES
270 LOCATE 20, 40: PRINT CHR$(17)
280 F$ = CHR$(2): E$ = CHR$(1): P$ = CHR$(5)
290 DR = 20: DC = 40: FR = 7: FC = 70: ER = FR: EC = FC
300 NDR = DR: NDC = DC: NFR = FR: NFC = FC: NER = NFR: NEC = NFC
310 PR = FR: PC = FC: NPR = PR: NPC = PC
320 '--INKEY
330 Z$ = INKEY$: LOCATE 23, 37: PRINT "SCORE = "; TOTAL
340 IF TOTAL >= 77777! THEN TOTAL = 77777!
350 LOCATE 23, 5: PRINT "Lives left = "; STRING$(LIFE, CHR$(16) + " ");
360 LOCATE 23, 53: PRINT "BULLETS = "; SHOT: LOCATE 23, 24: PRINT "LEVEL : "; LEVEL
370 IF Z$ = CHR$(27) THEN GOTO 3610
380 IF Z$ = CHR$(32) THEN SHOOTING = 1: GOTO 2190
390 IF Z$ = CHR$(0) + CHR$(72) THEN D$ = CHR$(6): WE$ = "U": GOTO 440
400 IF Z$ = CHR$(0) + CHR$(80) THEN D$ = CHR$(3): WE$ = "D": GOTO 440
410 IF Z$ = CHR$(0) + CHR$(75) THEN D$ = CHR$(17): WE$ = "L": GOTO 440
420 IF Z$ = CHR$(0) + CHR$(77) THEN D$ = CHR$(16): WE$ = "R"
430 '--
440 'DIGGER MOVES
450 '-
460 T = TIMER + SP: WHILE T > TIMER: WEND
470 IF WE$ = "U" THEN NDR = NDR - 1
480 IF WE$ = "D" THEN NDR = NDR + 1
490 IF WE$ = "L" THEN NDC = NDC - 1
500 IF WE$ = "R" THEN NDC = NDC + 1
510 IF SHOT > 10 THEN SHOT = 10
520 IF NDR = 22 OR NDR = 2 OR NDC = 1 OR NDC = 80 THEN GOTO 890
530 IF SCREEN(NDR, NDC) = 176 OR SCREEN(NDR, NDC) = 32 THEN 550
540 IF SCREEN(NDR, NDC) = 46 THEN 550 ELSE 570
550 LOCATE DR, DC: PRINT ".": LOCATE NDR, NDC: PRINT D$
560 DR = NDR: DC = NDC: GOTO 910
570 IF SCREEN(NDR, NDC) = 4 OR SCREEN(NDR, NDC) = 180 THEN 580 ELSE 610
580 IF SOU = 1 THEN SOUND 5000, .2
590 DIA = DIA + 1: TOTAL = TOTAL + 200
600 LOCATE DR, DC: PRINT ".": LOCATE NDR, NDC: PRINT D$: DR = NDR: DC = NDC
610 IF DIA >= IVAL THEN GOSUB 2140
620 IF SCREEN(NDR, NDC) = 19 THEN 630 ELSE 840
630 IF NDC = S1C AND NDR = S1R THEN 640 ELSE 700
640 T = TIMER + SP: WHILE T > TIMER: WEND: IF D$ = CHR$(17) THEN S1C = S1C - 1
650 IF D$ = CHR$(16) THEN S1C = S1C + 1
660 IF D$ = CHR$(3) THEN S1R = S1R + 1
670 IF D$ = CHR$(6) THEN S1R = S1R - 1
680 LOCATE S1R, S1C: PRINT CHR$(19)
690 GOTO 820
700 IF NDC = S3C AND NDR = S3R THEN 710 ELSE 760
710 T = TIMER + SP: WHILE T > TIMER: WEND: IF D$ = CHR$(17) THEN S3C = S3C - 1
720 IF D$ = CHR$(16) THEN S3C = S3C + 1
730 IF D$ = CHR$(3) THEN S3R = S3R + 1
740 IF D$ = CHR$(6) THEN S3R = S3R - 1
750 LOCATE S3R, S3C: PRINT CHR$(19): GOTO 820
760 IF NDC = S2C AND NDR = S2R THEN 770 ELSE 840
770 T = TIMER + SP: WHILE T > TIMER: WEND: IF D$ = CHR$(17) THEN S2C = S2C - 1
780 IF D$ = CHR$(16) THEN S2C = S2C + 1
790 IF D$ = CHR$(3) THEN S2R = S2R + 1
800 IF D$ = CHR$(6) THEN S2R = S2R - 1
810 LOCATE S2R, S2C: PRINT CHR$(19): GOTO 820
820 LOCATE NDR, NDC: PRINT D$: LOCATE DR, DC: PRINT CHR$(46)
830 DR = NDR: DC = NDC: GOTO 910
840 IF SCREEN(NDR, NDC) = 15 THEN TOTAL = TOTAL + 500: GOTO 850 ELSE 870
850 LOCATE NDR, NDC: PRINT D$: LOCATE DR, DC: PRINT "."
860 DR = NDR: DC = NDC: SHOT = SHOT + 2: GOTO 910
870 IF SCREEN(NDR, NDC) = 42 THEN TOTAL = TOTAL + 500: LOCATE NDR, NDC: PRINT D$: LOCATE DR, DC: PRINT " ": GOTO 2070
880 NDR = DR: NDC = DC: GOTO 910
890 NDR = DR: NDC = DC
900 '--
910 '--FACE 1
920 '--
930 IF EXTERMINATOR = 1 THEN GOTO 1100
940 T = TIMER + SP: WHILE T > TIMER: WEND:
950 IF SOU = 1 THEN SOUND 200, .1
960 ER = FR: EC = FC: NER = NFR: NEC = NFC
970 IF FR + 1 = DR AND FC = DC THEN GOTO 2660
980 IF FR = DR AND FC + 1 = DC THEN GOTO 2660
990 IF FR - 1 = DR AND FC = DC THEN GOTO 2660
1000 IF FR = DR AND FC - 1 = DC THEN GOTO 2660
1010 IF SCREEN(FR + 1, FC) = 46 THEN NFR = NFR + 1: GOTO 1080
1020 IF SCREEN(FR - 1, FC) = 46 THEN NFR = NFR - 1: GOTO 1080
1030 IF SCREEN(FR, FC + 1) = 46 THEN NFC = NFC + 1: GOTO 1080
1040 IF SCREEN(FR, FC - 1) = 46 THEN NFC = NFC - 1: GOTO 1080
1050 IF SCREEN(FR + 1, FC) = 15 OR SCREEN(FR, FC + 1) = 15 THEN 1070
1060 IF SCREEN(FR - 1, FC) = 15 OR SCREEN(FR, FC - 1) = 15 THEN 1070 ELSE 1100
1070 LOCATE NFR, NFC: PRINT F$: LOCATE FR, FC: PRINT CHR$(15): FR = NFR: FC = NFC
1080 LOCATE NFR, NFC: PRINT F$: LOCATE FR, FC: PRINT " ": FR = NFR: FC = NFC
1090 '
1100 'FACE 2
1110 '
1120 JAM = JAM + 1: IF JAM > 200 AND LEVEL >= 3 THEN 1130 ELSE 1320
1130 T = TIMER + SP: WHILE T > TIMER: WEND
1140 IF SOU = 1 THEN SOUND 3000, .5
1150 SNR = DR - PR: SNC = DC - PC
1160 IF SGN(SNR) = -1 AND SGN(SNC) = -1 THEN NPR = NPR - 1
1170 IF SGN(SNR) = -1 AND SGN(SNC) = 0 THEN NPR = NPR - 1
1180 IF SGN(SNR) = -1 AND SGN(SNC) = 1 THEN NPR = NPR - 1
1190 IF SGN(SNR) = 0 AND SGN(SNC) = -1 THEN NPC = NPC - 1
1200 IF SGN(SNR) = 0 AND SGN(SNC) = 1 THEN NPC = NPC + 1
1210 IF SGN(SNR) = 1 AND SGN(SNC) = -1 THEN NPR = NPR + 1
1220 IF SGN(SNR) = 1 AND SGN(SNC) = 0 THEN NPR = NPR + 1
1230 IF SGN(SNR) = 1 AND SGN(SNC) = 1 THEN NPR = NPR + 1
1240 IF SCREEN(NPR, NPC) = 176 OR SCREEN(NPR, NPC) = 46 THEN 1300 ELSE 1250
1250 IF NPR = DR AND NPC = DC THEN GOTO 2660
1260 IF SCREEN(NPR, NPC) = 4 THEN LOCATE PR, PC: COLOR 12: PRINT CHR$(4): COLOR 10 ELSE 1280
1270 LOCATE NPR, NPC: PRINT P$: PR = NPR: PC = NPC: GOTO 1320
1280 IF SCREEN(NPR, NPC) = 19 THEN 1290 ELSE 1300
1290 NPR = PR: NPC = PC: GOTO 1320
1300 LOCATE NPR, NPC: PRINT P$: LOCATE PR, PC: PRINT CHR$(176): PR = NPR: PC = NPC
1310 '--
1320 'EXTERMINATOR (FROM FACE) E$
1330 '-
1340 IF CHECKING = 1 THEN 1350 ELSE FST = FR: FST2 = FC
1350 IF FST = FR AND FST2 = FC THEN SAP = SAP + 1 ELSE SAP = 0: CHECKING = 0
1360 IF SAP >= 1 THEN CHECKING = 1
1370 IF SAP >= 10 THEN EXTERMINATOR = 1: LOCATE FR, FC: PRINT " ": GOTO 1380 ELSE GOTO 2190
1380 T = TIMER + SP: WHILE T > TIMER: WEND
1390 LOCATE ER, EC: PRINT E$
1400 GNR = DR - ER: GNC = DC - EC
1410 IF SGN(GNR) = -1 AND SGN(GNC) = -1 THEN NER = NER - 1: NEC = NEC - 1
1420 IF SGN(GNR) = -1 AND SGN(GNC) = 0 THEN NER = NER - 1
1430 IF SGN(GNR) = -1 AND SGN(GNC) = 1 THEN NER = NER - 1: NEC = NEC + 1
1440 IF SGN(GNR) = 0 AND SGN(GNC) = -1 THEN NEC = NEC - 1
1450 IF SGN(GNR) = 0 AND SGN(GNC) = 1 THEN NEC = NEC + 1
1460 IF SGN(GNR) = 1 AND SGN(GNC) = -1 THEN NER = NER + 1: NEC = NEC - 1
1470 IF SGN(GNR) = 1 AND SGN(GNC) = 0 THEN NER = NER + 1
1480 IF SGN(GNR) = 1 AND SGN(GNC) = 1 THEN NER = NER + 1: NEC = NEC + 1
1490 IF NER = DR AND NEC = DC THEN GOTO 2660
1500 IF SCREEN(NER, NEC) = 4 THEN 1510 ELSE 1520
1510 LOCATE ER, EC:  COLOR 12: PRINT CHR$(4): COLOR 10: LOCATE NER, NEC: PRINT E$: ER = NER: EC = NEC: FR = ER: FC = EC
1520 IF SCREEN(NER, NEC) = 46 THEN 1530 ELSE 1550
1530 EXTERMINATOR = 0: CHECKING = 0: SAP = 0: FST = 0: FST2 = 0: LOCATE ER, EC: PRINT " "
1540 FR = NER: FC = NEC: ER = NER: EC = NEC: NFR = FR: NFC = FC: GOTO 2190
1550 IF SCREEN(NER, NEC) = 32 OR SCREEN(NER, NEC) = 176 THEN 1560
1560 LOCATE NER, NEC: PRINT E$: LOCATE ER, EC: PRINT " "
1570 IF SOU = 1 THEN SOUND 400, .2
1580 ER = NER: EC = NEC: GOTO 2190
1590 NER = ER: NEC = EC
1600 '--
1610 'ENDING
1620 '--
1630 FOR I = 100 TO 2500 STEP 100:
1640 IF SOU = 1 THEN SOUND I, .2
1650 NEXT I
1660 COLOR 31
1670 LOCATE DR, DC: PRINT "R.I.P"
1680 FOR I = 5000 TO 100 STEP -100:
1690 IF SOU = 1 THEN SOUND I, .1
1700 NEXT I
1710 T = TIMER + 5: WHILE T > TIMER: WEND
1720 SD$ = INPUT$(1)
1730 SYSTEM
1740 '--
1750 'INTRODUCTION
1760 '--
1770 CLS : COLOR 12: LOCATE 2, 2: PRINT "A D V A N C E D  D I G G E R ";
1780 PRINT STRING$(3, CHR$(16))
1790 PRINT STRING$(80, "-")
1800 COLOR 30: LOCATE 4, 60: PRINT "By Navin.K.Ipe": PRINT : PRINT
1810 COLOR 10: PRINT "I N T R O D U C T I O N"
1820 COLOR 6: PRINT "-----------------------------"
1830 COLOR 10: PRINT "In this game you will have to cross 5 levels"
1840 PRINT "  Eating all the diamonds with the digger will do"
1850 PRINT "    the job.Faces will follow you.Contact with the"
1860 PRINT "      them is fatal.Use the spacebar to shoot and the"
1870 PRINT "        arrow keys to move the digger around.You have to"
1880 PRINT "          enter a portal ["; : COLOR 14
1890 COLOR 10: PRINT "*] to get into the next level."
1900 COLOR 12: PRINT : PRINT : PRINT "<<<<<Press a key>>>>>"
1910 COLOR 10: SD$ = INPUT$(1)
1920 RETURN
1930 DATA 8,10,10,10,10,13,12,10,12,13,12,16,14,10,14,13,14,16,14,19
1940 DATA 15,40,15,43,13,46,17,46,15,49,15,52,5,40,4,55,8,75,10,73,12,75
1950 DATA 7,15,9,15,11,15,9,12,3,18,11,12,11,18,4,37,4,43,6,40,8,40,14,37,14,43
1960 DATA 19,40,17,40,15,37,15,43,17,68,17,65,17,62,16,68,16,65,16,62,13,72,15,72,17,72
1970 DATA 14,74,16,74,18,74
1980 DATA 8,10,8,13,8,16,10,11,10,14,12,13,14,13,16,10,16,16,18,10,18,13,18,16,18,17,19,17
1990 DATA 6,15,6,18,6,21,6,24,6,27,8,16,8,19,8,22,8,25,8,28,9,29,9,30,10,30,11,30
2000 DATA 9,35,9,45,11,35,11,45,13,35,13,45,15,35,15,45,17,35,17,45,18,35,19,36
2010 DATA 9,75,11,75,13,75,15,75,9,73,11,73,13,73,15,73,15,72,15,73
2020 DATA 5,10,7,13,9,16,10,12,12,9,12,15,14,6,14,18,14,12,16,9,16,14,18,12,18,20,19,20
2030 DATA 20,17,10,14,14,25,6,34,6,37,6,40,6,43,6,46,4,40,8,40,11,34,11,37,11,40,11,43,13,40
2040 DATA 13,37,15,39,17,37,17,40,19,43,19,40,19,37,19,34,9,50,9,54,5,53,3,70,7,71
2050 DATA 3,72,5,78,9,78,13,78,17,78,21,78,7,74,11,74,15,74,19,74,19,76,19,77,19,78,9999,9999
2060 '-----
2070 '---CHANGE IN LEVEL
2080 '---
2090 LEVEL = LEVEL + 1: EXTERMINATOR = 0
2100 CHECKING = 0: FST = 0: FST2 = 0: SAP = 0
2110 T = 0: B = 0: JAM = 0: GHEE = 0: CLS
2120 IF LEVEL = 6 THEN 3410 ELSE 4060
2130 '---
2140 '-PORTAL
2150 '---
2160 COLOR 30: LOCATE 5, 2: PRINT CHR$(42)
2170 COLOR 10: RETURN
2180 '---
2190 '----SHOOTING
2200 '----
2210 IF SHO = 1 THEN 2240
2220 IF SHOOTING = 1 THEN GOTO 2230 ELSE 2410
2230 IF SHOT >= 1 THEN SHO = 1: DC$ = D$: DCC = DC: DRC = DR ELSE 2410
2240 IF DC$ = CHR$(6) THEN SHR = DRC - 1: SHC = DCC: NSHR = DRC: NSHC = DCC
2250 IF DC$ = CHR$(3) THEN SHR = DRC + 1: SHC = DCC: NSHR = DRC: NSHC = DCC
2260 IF DC$ = CHR$(17) THEN SHR = DRC: SHC = DCC - 1: NSHC = DCC: NSHR = DRC
2270 IF DC$ = CHR$(16) THEN SHR = DRC: SHC = DCC + 1: NSHC = DCC: NSHR = DRC
2280 IF SHR = ER AND SHC = EC THEN ER = 7: FR = 7: EC = 70: FC = 70: NEC = EC: NER = ER: NFC = FC: NFR = FR: LOCATE SHR, SHC: PRINT " ": TOTAL = TOTAL + 700
2290 IF SHR = FR AND SHC = FC THEN ER = 7: FR = 7: EC = 70: FC = 70: NEC = EC: NER = ER: NFC = FC: NFR = FR: LOCATE SHR, SHC: PRINT " ": TOTAL = TOTAL + 700
2300 IF SHR = PR AND SHC = PC THEN PR = 7: PC = 70: NPR = 7: NPC = 70: LOCATE SHR, SHC: PRINT " ": TOTAL = TOTAL + 700: GOTO 2320 ELSE 2320
2310 SHOT = SHOT - 1: SHO = 0: SHOOTING = 0: GOTO 2410
2320 IF SHR >= 22 OR SHR <= 2 OR SHC >= 80 OR SHC <= 1 THEN 2390
2330 IF SCREEN(SHR, SHC) = 176 THEN 2390
2340 IF SCREEN(SHR, SHC) = 46 OR SCREEN(SHR, SHC) = 32 THEN 2350 ELSE 2390
2350 LOCATE SHR, SHC: PRINT CHR$(248)
2360 IF SOU = 1 THEN SOUND 10000, .5
2370 LOCATE NSHR, NSHC: PRINT " "
2380 NSHC = SHC: NSHR = SHR: DCC = NSHC: DRC = NSHR: GOTO 2410
2390 LOCATE NSHR, NSHC: PRINT " ": GOTO 2310
2400 '------
2410  '---SACK FALLS
2420 '------
2430 IF SCREEN(S1R + 1, S1C) = 32 OR SCREEN(S1R + 1, S1C) = 46 THEN 2440 ELSE 2460
2440 NS1R = S1R + 1: LOCATE NS1R, S1C: PRINT S1$: LOCATE S1R, S1C: PRINT " "
2450 S1R = S1R + 1: S1$ = CHR$(15)
2460 IF S1R + 1 = FR AND S1C = FC OR S1R + 1 = ER AND S1C = EC THEN 2470 ELSE 2480
2470 GOSUB 3710: S1$ = CHR$(15): GOTO 320
2480 IF S1R + 1 = PR AND S1C = PC THEN 2490 ELSE 2500
2490 PR = 7: PC = 70: NPR = 7: NPC = 70: S1$ = CHR$(15)
2500 IF SCREEN(S2R + 1, S2C) = 32 OR SCREEN(S2R + 1, S2C) = 46 THEN 2510 ELSE 2530
2510 NS2R = S2R + 1: LOCATE NS2R, S2C: PRINT S2$: LOCATE S2R, S2C: PRINT " "
2520 S2R = S2R + 1: S2$ = CHR$(15)
2530 IF S2R + 1 = FR AND S2C = FC OR S2R + 1 = ER AND S2C = EC THEN 2540 ELSE 2550
2540 GOSUB 3710: S2$ = CHR$(15):
2550 IF S2R + 1 = PR AND S2C = PC THEN 2560 ELSE 2570
2560 PR = 7: PC = 70: NPR = 7: NPC = 70: S2$ = CHR$(15):
2570 IF SCREEN(S3R + 1, S3C) = 32 OR SCREEN(S3R + 1, S3C) = 46 THEN 2580 ELSE 2620
2580 NS3R = S3R + 1: LOCATE NS3R, S3C: PRINT S3$: LOCATE S3R, S3C: PRINT " "
2590 S3R = S3R + 1: S3$ = CHR$(15)
2600 IF S3R + 1 = FR AND S3C = FC OR S3R + 1 = ER AND S3C = EC THEN 2630 ELSE 2640
2610 GOSUB 3710: S3$ = CHR$(15):
2620 IF S3R + 1 = PR AND S3C = PC THEN 2640 ELSE 2640
2630 PR = 7: PC = 70: NPR = 7: NPC = 70: S3$ = CHR$(15):
2640 GOTO 320
2650 '-----
2660 '-----LIVES
2670 '-----
2680 IF LIFE = 0 THEN 1610 ELSE LIFE = LIFE - 1: WE$ = " "
2690 GOSUB 3710
2700 LOCATE 23, 5: PRINT "Lives left = "; STRING$(LIFE, CHR$(16) + " "); "      "
2710 FOR I = 100 TO 1000 STEP 200
2720 IF SOU = 1 THEN SOUND I, .2
2730 NEXT I
2740 T = TIMER + 5: WHILE T > TIMER: WEND: GOTO 260
2750 '----
2760 '----points
2770 '-----
2780 COLOR 30
2790 LOCATE 18, 30: PRINT "Your score = "; TOTAL
2800 RETURN
2810 '------
2820 '---VIDEO INTRO
2830 '-------
2840 CLS : LOCATE 6, 15: PRINT "DIGGER :": R = 6
2850 FOR I = 70 TO 31 STEP -2
2860 LOCATE R, I: PRINT CHR$(17): LOCATE R, I + 1: PRINT ".."
2870 FOR J = 1 TO 200: NEXT J
2880 IF SOU = 1 THEN SOUND 100, .5
2890 NEXT I
2900 T = TIMER + .05: WHILE T > TIMER: WEND: IF INKEY$ = CHR$(43) THEN GOTO 3400
2910 LOCATE 8, 15: PRINT "DIAMOND :": R = 8
2920 FOR I = 70 TO 31 STEP -2
2930 LOCATE R, I: COLOR 12: PRINT CHR$(4): COLOR 10: LOCATE R, I + 1: PRINT "  "
2940 T = TIMER + .05: WHILE T > TIMER: WEND
2950 IF SOU = 1 THEN SOUND 5000, .2
2960 NEXT I
2970 T = TIMER + .05: WHILE T > TIMER: WEND: IF INKEY$ = CHR$(43) THEN GOTO 3400
2980 LOCATE 10, 15: PRINT "WHITE MONSTER :": R = 10
2990 FOR I = 70 TO 31 STEP -2
3000 LOCATE R, I: PRINT CHR$(2): LOCATE R, I + 1: PRINT "  "
3010 T = TIMER + .05: WHILE T > TIMER: WEND
3020 IF SOU = 1 THEN SOUND 2000, .1
3030 NEXT I
3040 T = TIMER + .05: WHILE T > TIMER: WEND: IF INKEY$ = CHR$(43) THEN GOTO 3400
3050 LOCATE 12, 15: PRINT "BLACK MONSTER :": R = 12
3060 FOR I = 70 TO 31 STEP -2
3070 LOCATE R, I: PRINT CHR$(1): LOCATE R, I + 1: PRINT "  "
3080 T = TIMER + .05: WHILE T > TIMER: WEND
3090 IF SOU = 1 THEN SOUND 3000, .5
3100 NEXT I
3110 T = TIMER + .05: WHILE T > TIMER: WEND: IF INKEY$ = CHR$(43) THEN GOTO 3400
3120 LOCATE 14, 15: PRINT "EXTERMINATOR :": R = 14
3130 FOR I = 70 TO 31 STEP -2
3140 LOCATE R, I: PRINT CHR$(5): LOCATE R, I + 1: PRINT "  "
3150 T = TIMER + .05: WHILE T > TIMER: WEND
3160 IF SOU = 1 THEN SOUND 4000, .2
3170 NEXT I
3180 T = TIMER + .05: WHILE T > TIMER: WEND: IF INKEY$ = CHR$(43) THEN GOTO 3400
3190 LOCATE 16, 15: PRINT "PORTAL :": R = 16
3200 FOR I = 70 TO 31 STEP -2
3210 LOCATE R, I: PRINT CHR$(42): LOCATE R, I + 1: PRINT "  "
3220 T = TIMER + .05: WHILE T > TIMER: WEND
3230 IF SOU = 1 THEN SOUND 100, .1
3240 NEXT I
3250 T = TIMER + .05: WHILE T > TIMER: WEND: IF INKEY$ = CHR$(43) THEN GOTO 3400
3260 LOCATE 18, 15: PRINT "DROP BULLET :": R = 18
3270 FOR I = 70 TO 31 STEP -2
3280 LOCATE R, I: PRINT CHR$(19): LOCATE R, I + 1: PRINT "  "
3290 T = TIMER + .05: WHILE T > TIMER: WEND
3300 IF SOU = 1 THEN SOUND 300, .1
3310 NEXT I
3320 T = TIMER + .05: WHILE T > TIMER: WEND: IF INKEY$ = CHR$(43) THEN GOTO 3400
3330 LOCATE 20, 15: PRINT "BULLET CHARGE :": R = 20
3340 FOR I = 70 TO 31 STEP -2
3350 LOCATE R, I: PRINT CHR$(15): LOCATE R, I + 1: PRINT "  "
3360 T = TIMER + .05: WHILE T > TIMER: WEND
3370 IF SOU = 1 THEN SOUND 10000, .5
3380 NEXT I
3390 X$ = INPUT$(1): IF INKEY$ = CHR$(43) THEN GOTO 3400
3400 CLS : R = 0: I = 0: J = 0: T = 0: RETURN
3410 '----
3420 '--ULTIMATE END
3430 '----
3440 LOCATE 5, 20: PRINT STRING$(41, CHR$(4))
3450 R = 6: C = 20: C1 = 60
3460 LOCATE R, C: COLOR 12: PRINT CHR$(4): COLOR 10
3470 LOCATE R, C1: COLOR 12: PRINT CHR$(4): COLOR 10
3480 IF R = 16 THEN 3490 ELSE R = R + 1: GOTO 3460
3490 LOCATE 16, 20: PRINT STRING$(40, CHR$(4))
3500 LOCATE 6, 30: PRINT "YEP !! YOU HAVE DONE IT"
3510 LOCATE 7, 30: PRINT "~ ~ ~ ~ ~ ~ ~ ~ ~ ~ ~ ~"
3520 LOCATE 9, 24: PRINT "You have wonderfully taken all the"
3530 LOCATE 11, 24: PRINT "emeralds from under the earth and"
3540 LOCATE 13, 27: PRINT "presented it to King Authur."
3550 LOCATE 15, 26: PRINT "     EXCELLENT WORK !!"
3560 IF SOU = 1 THEN PLAY "MBL16T155"
3570 IF SOU = 1 THEN PLAY "o4e8e8c4e8e8c4c8g8c8d8c2"
3580 IF SOU = 1 THEN PLAY "f8f8f8f8f8e8e8e26e26e26g8g8f8d8c2"
3590 SD$ = INPUT$(1)
3600 SYSTEM
3610 '----
3620 '----EJECT
3630 '----
3640 COLOR 31: PRINT TAB(20); "-- YOU ARE EJECTED FROM THE DIGGER --"
3650 FOR I = 100 TO 2500 STEP 100
3660 IF SOU = 1 THEN SOUND I, .2
3670 NEXT
3680 SD$ = INPUT$(1)
3690 SYSTEM
3700 '----
3710 '--PRINTING SPACES
3720 '----
3730 LOCATE DR, DC: PRINT " "
3740 LOCATE NDR, NDC: PRINT " "
3750 LOCATE FR, FC: PRINT " "
3760 LOCATE NFR, NFC: PRINT " "
3770 LOCATE ER, EC: PRINT " "
3780 LOCATE NER, NEC: PRINT " "
3790 IF JAM > 100 AND LEVEL >= 3 THEN 3800 ELSE 3820
3800 LOCATE PR, PC: PRINT " "
3810 LOCATE NPR, NPC: PRINT " "
3820 FR = 7: FC = 70: ER = 7: EC = 70: PR = 7: PC = 70
3830 NFR = FR: NER = ER: NPR = PR: NFC = FC: NEC = EC: NPC = PC
3840 DR = 20: DC = 40
3850 RETURN
3860 '-----
3870 '--SOUNDING
3880 '-----
3890 SCREEN 0, 0, 0: CLS : COLOR 12: SOU = 1
3900 LOCATE 8, 28: PRINT "A D V A N C E D    D I G G E R"
3910 COLOR 11: LOCATE 10, 30: PRINT "(((((     SOUND     )))))"
3920 LOCATE 12, 25: PRINT "SOUND ON": LOCATE 12, 50: PRINT "SOUND OFF"
3930 COLOR 28: LOCATE 14, 29: PRINT CHR$(127)
3940 COLOR 10: LOCATE 14, 55: PRINT CHR$(127)
3950 PEEO$ = INKEY$
3960 IF PEEO$ = CHR$(13) THEN RETURN
3970 IF PEEO$ = CHR$(0) + CHR$(77) THEN SOU = 0
3980 IF PEEO$ = CHR$(0) + CHR$(75) THEN SOU = 1
3990 IF PEEO$ = CHR$(27) THEN SYSTEM
4000 IF SOU = 0 THEN COLOR 28: LOCATE 14, 55: GOTO 4010 ELSE 4020
4010 PRINT CHR$(127): COLOR 10: LOCATE 14, 29: PRINT CHR$(127)
4020 IF SOU = 1 THEN COLOR 10: LOCATE 14, 55: GOTO 4030 ELSE 4040
4030 PRINT CHR$(127): COLOR 28: LOCATE 14, 29: PRINT CHR$(127)
4040 GOTO 3950
4050 '--
4060 '--CHANGE IN LEVEL PRINT
4070 '--
4080 CLS : COLOR 13
4090 LOCATE 13, 30: PRINT CHR$(218); STRING$(11, CHR$(196)); CHR$(191)
4100 LOCATE 14, 30: PRINT CHR$(179); "LEVEL : "; LEVEL; CHR$(179)
4110 LOCATE 15, 30: PRINT CHR$(192); STRING$(11, CHR$(196)); CHR$(217)
4120 DER$ = INPUT$(1): COLOR 10
4130 GOTO 40
4140 '----
4150 '--SPEED SELECTION
4160 '----
4170 CLS : COLOR 10: LOCATE 10, 5
4180 PRINT "Select speed of Digger [40 Kmph, 50 Kmph, 60 Kmph]"
4190 LOCATE 11, 10: INPUT "Type 40,50 or 60 : "; SP
4200 IF SP = 40 THEN SP = .2: GOTO 4240
4210 IF SP = 50 THEN SP = .07: GOTO 4240
4220 IF SP = 60 THEN SP = .01: GOTO 4240
4230 GOTO 4150
4240 CLS : RETURN

