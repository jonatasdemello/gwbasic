10 CLS:PRINT@283,"WUMPUS I"
11 PRINT@409,"COPYRIGHT BY"
12 PRINT@470,"CREATIVE COMPUTING"
13 PRINT@536,"P.O. BOX 789-M"
14 PRINT@593,"MORRISTOWN, NEW JERSEY 07960"
15 FORI=1TO2000:NEXTI
20 REM  ADAPTED FOR SOL BY DAVID FOX - MARIN COMPUTER CENTER
25 REM  ADAPTED FOR TRS-80 LEVEL II BASIC BY DOUG BENEDICT
30 CLS
40 PRINT @ 394,"* * *  H U N T  T H E  W U M P U S  * * *"
50 DIM L0(21),L2(21),L3(21):L1=20
70 PRINT @ 832,""
80 INPUT "CARE FOR THE RULES";A$
90 IF MID$(A$,1,1)="Y" THEN GOSUB 850
100 PRINT @ 832,""
110 INPUT "PLEASE ENTER A RANDOM NUMBER";R1:CLS
120 IF R1=0 THEN R1=123
130 R1=(ABS(R1)+1)/10:IF R1>1 THEN 130
140 R1=RND(R1)
150 FOR I=1 TO L1:READ L3(I):NEXT I
160 DATA 148,185,160,120,105,205,228,248,268,285
170 DATA 283,260,240,220,203,303,328,368,383,340
180 FOR I=1 TO L1:READ L2(I):NEXT I
190 DATA 20508,010310,20412,30514,10406,50715,60817,10709
200 DATA 81018,20911,101219,31113,121420,41315,61416
210 DATA 151720,71618,91719,111820,131619
220 CLS:PRINT "THE WUMPUS IS RUNNING TO HIDE."
230 FOR I=1 TO L1:L0(I)=I:NEXT I
240 FOR I=L1 TO 2 STEP -1
250 J=RND(I)+1:K=L0(I):L0(I)=L0(J):L0(J)=K:NEXT I
260 W1=RND(L1):B1=RND(L1)
270 B2=RND(L1)
280 IF B1=B2 THEN 270
290 H1=RND(L1)
300 IF H1=B2 THEN 290
310 IF H1=B1 THEN 290
320 P1=RND(L1)
330 IF P1=W1 OR P1=B1 OR P1=B2 OR P1=H1 THEN 320
340 GOSUB 770
350 GOSUB 790:D5=D4
360 PRINT:IF P1<>W1 THEN 400
370 PRINT "LOOK OUT, IT'S THE WUMPUS ROOM!!!!"
380 PRINT "TOO LATE.  YOU'VE BEEN EATEN."
390 GOTO 660
400 IF P1<>B1 AND P1<>B2 THEN 420
410 PRINT "SUPER BATS!!!":PRINT "WHOOOOOSH":GOTO 320
420 IF P1<>H1 THEN 490
430 CLS:PRINT CHR$(23);"LOOK OUT!  BOTTOMLESS PIT!!"
450 PRINT "A I":GOSUB 2000:PRINT TAB(5);"E":GOSUB 2000:PRINT TAB(7);"E":GOSUB 2000
460 PRINT TAB(8);"E":GOSUB 2000:FOR I=1 TO 6:PRINT "        E":GOSUB 2000:NEXT I
480 GOTO 660
490 GOSUB 790
500 IF D4<D5 THEN PRINT "YOU ARE CLOSER TO THE WUMPUS."
510 IF D4>D5 THEN PRINT "YOU ARE FARTHER FROM THE WUMPUS."
520 D5=D4
530 P0=W1:GOSUB 750:IF K0 THEN PRINT "I SMELL A WUMPUS."
540 P0=B1:GOSUB 750:K1=K0:P0=B2:GOSUB 750
550 IF K0+K1 THEN PRINT "I HEAR BATS."
560 P0=H1:GOSUB 750:IF K0 THEN PRINT "I FEEL A DRAFT OF PITS."
570 PRINT:PRINT "THIS IS ROOM";L0(P1);"ADJACENT TO ";
580 PRINT L0(P5);",";L0(P6);",";L0(P7)
590 PRINT "SHOOT OR MOVE: ";
595 A1$=INKEY$:A1$=A1$:IF A1$="" THEN 595
596 PRINT A1$
600 IF A1$="M" THEN 700
610 IF A1$<>"S" THEN 590
620 INPUT "INTO WHICH ROOM";P2
630 GOSUB 730:IF K0<>1 THEN 580
640 IF P2<>W1 THEN 680
650 PRINT:PRINT "HURRAH!  ONE LESS WUMPUS.""
660 PRINT:PRINT:PRINT "CARE FOR ANOTHER GAME? ";
665 A1$=INKEY$:A1$=A1$:IF A1$="" THEN 665
670 IF A1$="Y" THEN 220 :ELSE 820
680 W1=RND(L1):PRINT:PRINT "MISSED!"
690 PRINT "WUMPUS IS MOVING.":GOTO 420
700 INPUT "MOVE TO";P2
710 GOSUB 730:IF K0 THEN P1=P2 :ELSE 580
720 GOSUB 770:GOTO 360
730 P0=L1+1:FOR I=1 TO L1:IF L0(I)=P2 THEN P0=I
740 NEXT I:P2=P0
750 IF P0=P5 OR P0=P6 OR P0=P7 THEN K0=1 :ELSE K0=0
760 RETURN
770 P4=L2(P1):P5=INT(P4/10000):P4=P4-10000*P5
780 P6=INT(P4/100):P7=P4-100*P6:RETURN
790 D6=L3(P1):D7=L3(W1):D0=0:FOR I=1 TO 3
800 D8=INT(D6/10):D9=INT(D7/10):D6=D6-D7-10*(D8-D9)
810 D0=D0+D6*D6:D6=D8:D7=D9:NEXT I:D4=SQR(D0):RETURN
820 CLS:PRINT @ 335,"THANK YOU FOR PLAYING WITH ME!!"
830 REM RUN"MENU"
840 PRINT:PRINT"TO LOAD ANOTHER PROGRAM, TYPE 'CLOAD'.":PRINT:END
850 REM  RULES
860 CLS
870 PRINT "THE WUMPUS IS A SHY, TIMID PERSON-EATING ANIMAL.  IT LIVES IN"
880 PRINT "ONE OF 20 CONNECTING ROOMS.  THE OBJECT IS TO FIND AND SHOOT"
890 PRINT "THE WUMPUS WITH YOUR BOWS AND ARROWS.":PRINT
900 PRINT "IN SOME OF THE ROOMS ARE SUPER BATS.  IF YOU WALK INTO THEIR"
910 PRINT "ROOM, THEY WILL PICK YOU UP AND DROP YOU INTO ONE OF THE ROOMS"
920 PRINT "RANDOMLY.":PRINT
930 PRINT "ALSO SOME OF THE ROOMS CONTAIN BOTTOMLESS PITS.  FALLING INTO"
940 PRINT "ONE WILL CAUSE A SUDDEN ATTACK OF DEATH.":PRINT
950 PRINT "THE WUMPUS WILL STAY IN ONE PLACE UNLESS DISTURBED BY THE SOUND"
960 PRINT "OF AN ARROW SHOOTING THROUGH A ROOM.":PRINT @ 896,""
970 INPUT "PRESS 'ENTER' TO CONTINUE";A$
975 CLS
980 PRINT "TO HELP YOU IN YOUR QUEST ARE THE FOLLOWING AIDS:"
990 PRINT "1) IF YOU ARE IN A ROOM ADJOINING THE WUMPUS'S ROOM,"
1000 PRINT "     'I SMELL A WUMPUS' IS PRINTED OUT."
1010 PRINT "2) IF YOU ARE IN A ROOM ADJOINING THE BAT'S ROOM,"
1020 PRINT "     'I HEAR BATS' IS PRINTED OUT."
1030 PRINT "3) IF AN ADJOINING ROOM CONTAINS BOTTOMLESS PITS,"
1040 PRINT "     'I FEEL A DRAFT OF PITS' IS PRINTED OUT."
1050 PRINT "4) AS YOU MOVE, YOU ARE TOLD WHETHER YOUR NEW ROOM NUMBER IS"
1060 PRINT "   CLOSER OR FARTHER TO THE WUMPUS'S ROOM NUMBER THAN YOUR LAST"
1070 PRINT "   ROOM NUMBER.  IF THEY ARE THE SAME DISTANCE, NO MESSAGE IS"
1080 PRINT "   PRINTED.  THE NUMBERING OF THE ROOMS IS RANDOM AND SOME OF"
1090 PRINT "   THE PASSAGES GO ONLY IN ONE DIRECTION."
1100 PRINT:PRINT "HAVE FUN AND GOOD HUNTING!"
1110 RETURN
2000 FOR HL=1 TO 60:NEXT:RETURN
