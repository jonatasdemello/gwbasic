10 KEY OFF
20 'CHESS C.4  FROM "CREATIVE COMPUTING", DEC. 1981, PAGE 276
21 'ENTERED AND ALTERED BY MIKE STAFFORD
30 DEFINT A-Z
40 DIM C(64)
50 A=0
60 B=0
70 X=0
80 Y=0
90 S=0
100 A0=0
110 T=0
120 A8=0
130 A1=0
140 A2=0
150 A3=0
160 A4=0
170 B1=0
180 B6!=0
190 H=0
200 M=0
210 N=0
220 P=0
230 W=0
240 A5=0
250 I=1
260 CLS
270 INPUT "DO YOU WANT INSTRUCTIONS ";INs$
280 INs$=LEFT$(INs$,1)
285 IF INs$>="a" AND INs$<="z" THEN INs$=CHR$(ASC(INs$)-32)
290 IF INs$="Y" THEN GOSUB 3170
300 CL$="N"
310 CLS
320 INPUT "YOUR NAME IS ";B$
330 IF B$="" THEN B$=" HUMAN   " ELSE B$=LEFT$(B$,9)
340 CLS
350 INPUT " LEVEL OF PLAY";B8!
360 IF B8!=0 THEN B8!=1: B7!=1+RND/2 ELSE B7!=(B8!+1+RND)/2
370 CLS
380 PRINT " DO YOU WANT WHITE ";B$;" ";
390 INPUT C$
400 GOSUB 2590
410 IF LEFT$(C$,1)<>"N" THEN GOSUB 2740: GOTO 740
420 GOSUB 3430
430 IF CL$="Y" THEN LOCATE 5,64: PRINT "START ";TIME$;: LOCATE 6,64: PRINT STRING$(14," ");
440 F!=-99
450 A0=0
460 FOR J=1 TO 8
470 FOR K=1 TO 8
480 IF A(J,K)=99 THEN A6=J: A7=K
490 NEXT K
500 NEXT J
510 FOR X=1 TO 8
520 FOR Y=1 TO 8
530 IF A(X,Y)<0 THEN GOSUB 910: IF F!>=B7! THEN 580
540 NEXT Y
550 NEXT X
560 IF F!>=-9 THEN 580 ELSE GOSUB 2740
570 LOCATE 4,64: PRINT"I CONCEDE":GOTO 4230
580 A(R,U)=A(E,Q)
590 A(E,Q)=1
600 IF A(R,U)=-2 AND U=1 THEN A(R,U)=-9
610 X=R
620 Y=U
630 A0=4
640 GOSUB 910
650 LOCATE 4,64: PRINT "IS MY MOVE     ";: LOCATE 3,64: PRINT CHR$(E+64);Q;"- ";CHR$(R+64);U;
660 IF CL$="Y" THEN LOCATE 6,64: PRINT "STOP ";TIME$;
670 X=E
680 Y=Q
690 GOSUB 2840
700 X=R
710 Y=U
720 GOSUB 2840
730 IF C=1 THEN LOCATE 4,64: PRINT "CHECK ";B$;: C=0
740 IF X$="S" THEN GOSUB 3790:B7!=3*RND:LOCATE 1,64:PRINT "SELF-PLAYING";:GOSUB 4430:GOTO 440 ELSE LOCATE 2,64:PRINT "MOVE ";B$;:GOSUB 3500:D=0:B4=A:B9=B:B5=0:IF A(X,Y)=2 AND Y=5 AND B=6 AND A(A,B)=1 AND ABS(A-X)=1 THEN B5=1:GOTO 770
750 IF X$="K" OR X$="Q" THEN GOSUB 2740: LOCATE 2,65: PRINT STRING$(14," ");:GOTO 440
760 IF X$="S" THEN CL$="N": GOTO 740 ELSE A0=3: GOSUB 900: IF D=0 THEN LOCATE 3,64: PRINT "ILLEGAL";: GOTO 740
770 LOCATE 2,64: PRINT "OK            ";
780 A=B4
790 B=B9
800 A(A,B)=A(X,Y)
810 A(X,Y)=1
820 GOSUB 2840
830 X=A
840 Y=B
850 GOSUB 2840
860 IF N$="N" THEN GOSUB 2740: N$=" "
870 IF A(A,B)=2 AND B=8 THEN LOCATE 2,64: PRINT "WHAT PIECE";: GOSUB 4350: X=A: Y=B: GOSUB 2840: LOCATE 2,64: PRINT STRING$(14," ");: LOCATE 3,64: PRINT STRING$(14," ");
880 IF B5=1 THEN  X=A: Y=B-1: GOSUB 2840
890 GOTO 440
900 LOCATE 1,76: PRINT "$";: ON A(X,Y) GOTO 10,1520,10,1270,1070,10,920,10,1070:GOTO 1460
910 ON -A(X,Y) GOTO 10,1640,10,1270,1070,10,920,10,1070: GOTO 1460
920 B=Y
930 FOR A=X+1 TO 8
940 GOSUB 2040
950 IF S=0 THEN NEXT
960 FOR A=X-1 TO 1 STEP -1
970 GOSUB 2040
980 IF S=0 THEN NEXT
990 A=X
1000 FOR B=Y+1 TO 8
1010 GOSUB 2040
1020 IF S=0 THEN NEXT
1030 FOR B=Y-1 TO 1 STEP -1
1040 GOSUB 2040
1050 IF S=0 THEN NEXT
1060  RETURN
1070 B=Y
1080 FOR A=X+1 TO 8
1090 B=B+1
1100 GOSUB 2040
1110 IF S=0 THEN NEXT
1120 B=Y
1130 FOR A=X-1 TO 1 STEP -1
1140 B=B-1
1150 GOSUB 2040
1160 IF S=0 THEN NEXT
1170 B=Y
1180 FOR A=X-1 TO 1 STEP -1
1190 B=B+1
1200 GOSUB 2040
1210 IF S=0 THEN NEXT
1220 B=Y
1230 FOR A=X+1 TO 8
1231 B=B-1
1240 GOSUB 2040
1250 IF S=0 THEN NEXT
1260 IF ABS(A(X,Y))=9 THEN 920 ELSE RETURN
1270 A=X+2
1280 B=Y+1
1290 IF A<9 AND B<9 THEN GOSUB 2060
1300 B=B-2
1310 IF B>0 AND A<9 THEN GOSUB 2060
1320 A=A-4
1330 IF A>0 AND B>0 THEN GOSUB 2060
1340 B=B+2
1350 IF B<9 AND A>0 THEN GOSUB 2060
1360 A=A+1
1370 B=B+1
1380 IF A>0 AND A<9 AND B<9 THEN GOSUB 2060
1390 B=B-4
1400 IF B>0 AND A>0 AND A<9 THEN GOSUB 2060
1410 A=A+2
1420 IF A>0 AND A<9 AND B>0 THEN GOSUB 2060
1430 B=B+4
1440 IF B<9 AND A>0 AND A<9 THEN GOSUB 2060
1450 RETURN
1460 FOR A=X-1 TO X+1
1470 FOR B=Y-1 TO Y+1
1480 IF A(A,B)<>0 THEN GOSUB 2060
1490 NEXT
1500 NEXT
1510 RETURN
1520 A=X
1530 IF Y>2 THEN 1560
1540 B=Y+1
1550 IF A(A,B)=1 THEN GOSUB 2070: B=B+1: IF A(A,B)=1 THEN GOSUB 2070: GOTO 1580 ELSE GOTO 1580 ELSE GOTO 1580
1560 B=Y+1
1570 IF A(A,B)=1 THEN GOSUB 2070
1580 A=X+1
1590 B=Y+1
1600 IF A(A,B)<0 THEN GOSUB 2070
1610 A=A-2
1620 IF A(A,B)<0 THEN GOSUB 2070
1630 RETURN
1640 A=X
1650 IF Y<7 THEN 1680
1660 B=Y-1
1670 IF A(A,B)=1 THEN GOSUB 2070: B=B-1: IF A(A,B)=1 THEN GOSUB 2070: GOTO 1700 ELSE GOTO 1700 ELSE GOTO 1700
1680 B=Y-1
1690 IF A(A,B)=1 THEN GOSUB 2070
1700 A=X-1
1710 B=Y-1
1720 IF A(A,B)>1 THEN GOSUB 2070
1730 A=A+2
1740 IF A(A,B)>1 THEN GOSUB 2070
1750 RETURN
1760 T=A(A,B)
1770 IF T=-99 THEN B1=T: RETURN
1780 A5=S
1790 IF ABS(T)<=A(X,Y) THEN A(A,B)=A(X,Y): A(X,Y)=1: GOTO 1820
1800 IF T<B1 THEN B1=T
1805 S=A5
1810 RETURN
1820 A1=X
1830 A2=Y
1840 A3=A
1850 A4=B
1860 A8=T
1870 A0=2
1880 FOR X=1 TO 8
1890 FOR Y=1 TO 8
1900 IF A(X,Y)<0 THEN GOSUB 910: IF T=0 THEN 1930
1910 NEXT
1920 NEXT
1930 X=A1
1940 Y=A2
1950 A=A3
1960 B=A4
1970 A0=5
1980 A(X,Y)=A(A,B)
1990 A(A,B)=A8
2000 GOTO 1800
2010 '***************************************************************
2020 '* THIS SUBROUTINE CHECKS THE LEGALITY OF A MOVE
2030 '***************************************************************
2040 S=0
2050 IF A(A,B)=1 THEN 2070 ELSE IF A(A,B)=0 THEN S=1: RETURN ELSE IF SGN(A(A,B))=SGN(A(X,Y)) THEN S=1: RETURN ELSE S=1: GOTO 2070
2060 IF A(A,B)=1 THEN GOTO 2070 ELSE IF SGN(A(A,B))=SGN(A(X,Y)) THEN RETURN
2070 LOCATE 1,76: PRINT "  ";: ON A0 GOTO 2090,2110,2130,2150,2170
2080 GOTO 2180
2090 IF A6=A AND A7=B THEN B1=B1+1
2100 RETURN
2110 IF A3=A AND A4=B THEN T=0
2120 RETURN
2130 IF B4=A AND B9=B THEN D=1
2140 RETURN
2150 IF A6=A AND A7=B THEN C=1
2160 RETURN
2170 IF A(A,B)<0 THEN 1760 ELSE RETURN
2180 B3=S
2190 W=X
2200 M=Y
2210 N=A
2220 H=B
2230 P=A(A,B)
2240 A(A,B)=A(X,Y)
2250 A(X,Y)=1
2260 B1=0
2270 A0=5
2280 FOR X=1 TO 8
2290 FOR Y=1 TO 8
2300 IF A(X,Y)>1 THEN GOSUB 900
2310 NEXT Y
2320 NEXT X
2330 X=N
2340 Y=H
2350 A0=1
2360 GOSUB 910
2370 A0=0
2380 S=B3
2390 X=W
2400 Y=M
2410 A=N
2420 B=H
2430 A(X,Y)=A(A,B)
2440 A(A,B)=P
2450 B6!=1/(ABS(4.5-A)+ABS(4.5-B)+1)
2460 IF A(X,Y)<-2 AND A(X,Y)>-9 THEN B6!=B6!+1/(ABS(A6-A)+ABS(A7-B)+5)+RND/15
2470 G!=P+B1+B6!
2480 IF P=99 THEN GOSUB 2740: LOCATE 4,64: PRINT "MATE ";B$; : GOTO 4230
2490 IF G!<=F! THEN RETURN
2500 F!=G!
2510 E=X
2520 Q=Y
2530 R=A
2540 U=B
2550 RETURN
2560 '***************************************************************
2570 '*  INITIALIZE POSITIONS ON BOARD
2580 '***************************************************************
2590 CLS
2600 FOR Y=1 TO 8
2610 FOR X=1 TO 8
2620 READ A(X,Y)
2630 NEXT X
2640 NEXT Y
2650 DATA 7,4,5,9,99,5,4,7
2660 DATA 2,2,2,2,2,2,2,2
2670 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
2680 DATA -2,-2,-2,-2,-2,-2,-2,-2
2690 DATA -7,-4,-5,-9,-99,-5,-4,-7
2700 RETURN
2710 '***************************************************************
2720 '*  DRAWS BOARD AND PIECES
2730 '***************************************************************
2740 FOR Y=1 TO 8
2750 FOR X=1 TO 8
2760 GOSUB 2840
2770 NEXT X
2780 NEXT Y
2785 GOSUB 4090
2790 RETURN
2800 '***************************************************************
2810 '*  SUBROUTINE TO DRAW A SQUARE AND THE PIECE ON IT.
2820 '*  SQUARE IS IDENTIFIED BY THE COORDINATES X,Y
2830 '***************************************************************
2840 IF I=-1 THEN 2870
2850 IF (INT((X+Y)/2))*2=X+Y THEN SQ$=CHR$(176) ELSE SQ$=CHR$(178)
2860 GOTO 2880
2870 IF (INT((X+Y)/2))*2=X+Y THEN SQ$=CHR$(178) ELSE SQ$=CHR$(176)
2880 YC=24-3*Y
2890 XC=2+7*(X-1)
2900 TEMP=ABS(A(X,Y))
2910 FOR YS=1 TO 3
2920 FOR XS=1 TO 7
2930 SQCH$=SQ$
2940 IF XS<>4 THEN 3080
2950 IF A(X,Y)=1 THEN GOTO 3080
2960 IF YS=1 AND TEMP<9 THEN 3080
2970 IF I*A(X,Y)<0 THEN COLOR 0,7
2980 IF YS<>1 THEN 3010
2990 IF TEMP=9 THEN SQCH$=CHR$(203) ELSE SQCH$=CHR$(206)
3000 GOTO 3080
3010 IF YS=3 THEN 3070
3020 IF TEMP=2 THEN SQCH$=CHR$(194): GOTO 3080
3030 IF TEMP=7 THEN SQCH$=CHR$(203): GOTO 3080
3040 IF TEMP=4 THEN SQCH$=CHR$(187): GOTO 3080
3050 IF TEMP=5 THEN SQCH$=CHR$(206): ELSE SQCH$=CHR$(186)
3060 GOTO 3080
3070 IF TEMP=2 THEN SQCH$=CHR$(193) ELSE SQCH$=CHR$(202)
3080 LOCATE YC+YS,XC+XS
3090 PRINT SQCH$;
3100 COLOR 7,0
3110 NEXT XS
3120 NEXT YS
3130 RETURN
3140 '***************************************************************
3150 '*  INSTRUCTIONS
3160 '***************************************************************
3170 CLS
3180 PRINT " 1. SPECIAL COMMANDS DURING PLAYER'S MOVE ARE:"
3190 PRINT "         'K' - TO CASTLE KING SIDE."
3200 PRINT "         'Q' - TO CASTLE QUEEN SIDE."
3210 PRINT "         'X' - TO EXCHANGE PIECES AND LET COMPUTER PLAY."
3220 PRINT "         'S' - TO LET THE COMPUTER PLAY BY ITSELF."
3230 PRINT "         'M' - TO MODIFY THE BOARD ENTER THE SQUARE FOLLOWED BY 'C','P',"
3330 PRINT "               OR 'E' FOR COMPUTER, PLAYER OR EMPTY AND 'S', 'P', 'N', 'B',"
3339 PRINT "               'R', 'Q', OR 'K' FOR SQUARE, PAWN, KNIGHT, BISHOP, ROOK, QUEEN,"
3340 PRINT "               OR QUEEN."
3341 PRINT "         'Z' - TO ESCAPE FROM MODIFY AND LET THE COMPUTER MOVE."
3342 PRINT "         'I' - TO GET INSTRUCTIONS AGAIN."
3343 PRINT "         'L' - TO LOOK AT OR CHANGE LEVEL OF PLAY."
3350 PRINT "               OR KING."
3360 PRINT " 2. TO PROMOTE TO P, N, B, R, OR Q ENTER THE LETTER WHEN PROMPTED."
3370 PRINT " 3. TO CAPTURE 'EN PASSANT' SPECIFY THE 'FROM' - 'TO' SQUARES."
3380 PRINT " 4. LEVELS OF PLAY ARE FROM 01 TO 24."
3390 PRINT " 5. COMPUTER ASSUMES MATE IF KING IS LEFT IN CHECK, SO BE SURE TO WATCH FOR"
3391 PRINT "    THE CHECK MESSAGE."
3392 PRINT " 6. IF A MOVE IS ILLEGAL OR AN ENTRY ERROR IS MADE SIMPLY TYPE IN A NEW MOVE."
3393 PRINT "    A MOVE MAY BE STARTED OVER BY FORCING AN ERROR. (E.G. '9').
3394 PRINT " 7. YOU HAVE A CHOICE OF WHITE OR BLACK.  THE COMPUTER WILL ALWAYS BE AT THE"
3395 PRINT "    TOP OF THE SCREEN."
3410 INPUT "HIT ENTER TO CONTINUE";EN$
3420 RETURN
3430 I=-1
3440 A(4,1)=99
3450 A(5,1)=9
3460 A(4,8)=-99
3470 A(5,8)=-9
3480 GOSUB 2740
3490 RETURN
3500 LOCATE 1,64: PRINT STRING$(14," ");
3510  X$=INKEY$
3520 IF X$="N" THEN N$=X$
3530 IF X$>="A" AND X$<="H" THEN X=VAL(CHR$(ASC(X$)-16)): GOTO 3660
3540 IF X$="N" THEN 4090
3550 IF X$="K" AND A(5,1)=99 AND A(8,1)=7 THEN A(5,1)=1: A(8,1)=1: A(7,1)=99: A(6,1)=7: GOTO 3750
3560 IF X$="Q" AND A(5,1)=99 AND A(1,1)=7 THEN A(5,1)=1: A(1,1)=1: A(3,1)=99: A(4,1)=7: GOTO 3750
3570 IF X$="K" AND A(4,1)=99 AND A(1,1)=7 THEN A(4,1)=1: A(1,1)=1: A(2,1)=99: A(3,1)=7: GOTO 3750
3580 IF X$="Q" AND A(4,1)=99 AND A(8,1)=7 THEN A(4,1)=1: A(8,1)=1: A(6,1)=99: A(5,1)=7: GOTO 3750
3590 IF X$="X" THEN GOSUB 3790:LOCATE 1,64: PRINT "EXCHANGING   ";: GOSUB 4430: IF CL$="Y" THEN LOCATE 5,60: PRINT "START ";TIME$;: GOTO 440 ELSE GOTO 440
3610 IF X$="S" THEN 3780
3620 IF X$="M" THEN GOSUB 3830
3630 IF X$="I" THEN GOSUB 3170: CLS: GOSUB 2740: LOCATE 1,64: PRINT "MOVE ";B$;
3640 IF X$="L" THEN GOSUB 4140
3650 GOTO 3510
3660 LOCATE 1,65: PRINT X$;
3670 Y=VAL(INKEY$): IF Y=0 THEN 3670
3680 LOCATE 1,66: PRINT Y;
3690 LOCATE 1,69: PRINT "-";
3700 IF A(X,Y)<2 OR A(X,Y)>99 THEN LOCATE 4,64: PRINT "ENTRY ERROR  ";: GOTO 3500
3710 A$=INKEY$: IF A$<"A" OR A$>"H" THEN 3710 ELSE A=VAL(CHR$(ASC(A$)-16))
3720 LOCATE 1,71: PRINT A$
3730 B=VAL(INKEY$)
3740 IF B=0 THEN 3730 ELSE LOCATE 1,72: PRINT  B;
3750 IF X$="K" OR X$="Q" THEN LOCATE 1,64: PRINT "CSTL. ";X$;"-SIDE";
3760 GOSUB 3790
3770 IF CL$="Y" THEN LOCATE 5,64: PRINT "START ";TIME$;
3780 RETURN
3790 FOR J=2 TO 7
3800 LOCATE J,64: PRINT STRING$(16," ");
3810 NEXT J
3820 RETURN
3830 GOSUB 3790
3840 LOCATE 1,64: PRINT "MODIFYING     ";
3850 GOSUB 4060
3860 IF M$<"A" OR M$>"H" THEN 3850
3870 X=VAL(CHR$(ASC(M$)-16))
3880 LOCATE 2,64: PRINT M$;
3890 M$=INKEY$: IF M$<"1" OR M$>"8" THEN 3890
3900 Y=VAL(M$)
3910 LOCATE 2,66: PRINT M$; " = ";
3920 M$=INKEY$: IF M$="C" OR M$="P" OR M$="E" THEN S1$=M$: LOCATE 2,70: PRINT M$; ELSE GOTO 3920
3930 M$=INKEY$: IF M$="S" OR M$="P" OR M$="N" OR M$="B" OR M$="R" OR M$="Q" OR M$="K" THEN V$=M$: LOCATE 2,72: PRINT M$; ELSE GOTO 3930
3940 IF V$="P" THEN V1=2
3950 IF V$="N" THEN V1=4
3960 IF V$="B" THEN V1=5
3970 IF V$="R" THEN V1=7
3980 IF V$="Q" THEN V1=9
3990 IF V$="K" THEN V1=99
4000 IF V$="S" THEN V1=1
4010 IF S1$="C" AND V1<>1 THEN V1=-V1
4020 A(X,Y)=V1
4030 IF S1$="E" THEN A(X,Y)=1
4040 GOSUB 2840
4050 GOTO 3830
4060 M$=INKEY$: IF M$="N" THEN GOSUB 4090: GOTO 4060 ELSE IF M$="Z" THEN GOSUB 3790: LOCATE 1,64: PRINT STRING$(14," ");: GOSUB 2740 ELSE RETURN
4070 IF CL$="Y" THEN LOCATE 5,60: PRINT "START "; TIME$;
4080 GOTO 440
4090 FOR J=1 TO 8
4100 LOCATE (J*3)-1,1: PRINT RIGHT$(STR$(9-J),1);
4110 LOCATE 25,(J*7)-1: PRINT CHR$(J+64);
4120 NEXT J
4130 RETURN
4140 GOSUB 3790
4150 LOCATE 1,64: PRINT "LEVEL=";B8!;"   ";
4160 LOCATE 2,64: PRINT "CHANGE LEVELS?";
4170 C$=INKEY$: IF C$="N" THEN 4225 ELSE IF C$="Y" THEN LOCATE 1,71: PRINT STRING$(7," ");: GOSUB 3790 ELSE GOTO 4170
4180 L1$=INKEY$: IF L1$="" THEN 4180 ELSE IF L1$>="0" AND L1$<"9" THEN LOCATE 1,71: PRINT L1$ ELSE GOTO 4180
4190 L2$=INKEY$: IF L2$="" THEN 4190 ELSE IF L2$<"0" OR L2$>"9" THEN L2$=L1$: L1$="0"
4200 B8!=VAL(L1$)*10+VAL(L2$)
4210 B7!=B8!/2
4220 LOCATE 1,71: PRINT L1$;L2$;
4225 GOSUB 3790: LOCATE 1,64: PRINT STRING$(14," ");: LOCATE 2,64: PRINT "MOVE ";B$;: RETURN
4230 IF CL$="Y" THEN LOCATE 6,64: PRINT "STOP "; TIME$;
4240 LOCATE 9,62: PRINT "HIT 'P' TO PLAY";
4250 LOCATE 10,62: PRINT "OR 'B' FOR BASIC.";
4260 A$=INKEY$: IF A$="" THEN 4260 ELSE IF A$="B" THEN CLS: END ELSE IF A$="P" THEN RUN ELSE GOTO 4260
4280 GOSUB 3790
4290 LOCATE 1,64: PRINT STRING$(14," ");
4300 LOCATE 2,64: PRINT "MOVE ";B$;
4310 RETURN
4320 '*******************************************************
4330 '*  PROMOTION ROUTINE
4340 '*******************************************************
4350 LOCATE 3,64: PRINT "P, N, B, R, Q";
4360 Z$=INKEY$: IF Z$="P" THEN A(A,B)=2: GOTO 4420
4370 IF Z$="N" THEN A(A,B)=4: GOTO 4420
4380 IF Z$="B" THEN A(A,B)=5: GOTO 4420
4390 IF Z$="R" THEN A(A,B)=7: GOTO 4420
4400 IF Z$="Q" THEN A(A,B)=9: GOTO 4420
4410 GOTO 4360
4420 RETURN
4430 I=-1
4440 GOSUB 4470
4450 GOSUB 2740
4460 RETURN
4470 L=0
4480 FOR J=1 TO 8
4490 FOR K=1 TO 8
4500 L=L+1
4510 IF A(J,K)<>1 THEN C(L)=-A(J,K) ELSE C(L)=A(J,K)
4520 NEXT K
4530 NEXT J
4540 L=65
4550 FOR J=1 TO 8
4560 FOR K=1 TO 8
4570 L=L-1
4580 A(J,K)=C(L)
4590 NEXT K
4600 NEXT J
4610 RETURN
4620 END
